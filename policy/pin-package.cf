body file control{
      namespace => "apt";
}
bundle agent package_version_priority_pinned( name, version, priority, state )
# @brief Ensure that `name` is pinned to `version` for the apt package manager or not based on the desired state
# @param `name` The name of the package to pin, case matters.
# @param `version` The version of the package to pin
# @param `priority` The version of the package to pin
# @param `state` The desired state of the package pin (present|absent), case does not matter. Note: the version does not matter when absent is specified.
{
  vars:
      "default_preferences_dir"
        string => "/etc/apt/preferences.d";

      "preferences_dir"
        string => "$(default_preferences_dir)",
        unless => isvariable( "$(this.promiser)" ),
        comment => "We allow the apt preferences.d directory to be overridden for ease of testing.";

  files:
      "$(preferences_dir)/."
        perms => default:mog( "755", "root", "root" ),
        if => regcmp( "(?i)present", "$(state)" ),
        comment => concat( "If we are promising that a package",
                           " should be pinned, the configuration",
                           " directory should exist." );

      "$(preferences_dir)/$(name)"
        perms => default:mog( "644", "root", "root" ),
        content => concat( "# Managed by CFEngine", "$(const.n)",
                           "Package: $(name)", "$(const.n)",
                           "Pin: version $(version)", "$(const.n)",
                           "Pin-Priority: $(priority)", "$(const.n)" ),
        if => regcmp( "(?i)present", "$(state)" );

      "$(preferences_dir)/$(name)"
        delete => default:tidy,
        if => regcmp( "(?i)absent", "$(state)" );
}
